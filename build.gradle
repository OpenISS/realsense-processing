plugins {
    id 'java-library'
    id 'org.bytedeco.gradle-javacpp-platform' version "1.5.7"
}

group 'ch.bildspur'
version '2.4.3'

sourceCompatibility = 1.8

def rsVersion = 'v2.44.0-0.4.3'

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

configurations {
    jar.archiveName = outputName + '.jar'
}

javadoc {
    source = sourceSets.main.allJava
}

dependencies {
    // release
    implementation "com.github.cansik:librealsense-java:$rsVersion"

    // compile
    testImplementation group: 'junit', name: 'junit', version: '4.13.1'

    // processing
    implementation fileTree(include: ["core.jar", "jogl-all-main.jar", "gluegen-rt-main.jar"], dir: 'core-libs')
}

task fatJar(type: Jar) {
    archiveFileName = "$outputName-complete.jar"
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    dependsOn configurations.runtimeClasspath
    from {
        (configurations.runtimeClasspath).filter( {! (it.name =~ /core.jar/ ||
                it.name =~ /jogl-all-main.jar/ ||
                it.name =~ /gluegen-rt-main.jar/)}).collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
    with jar
}

// add processing library support
apply from: "processing-library.gradle"